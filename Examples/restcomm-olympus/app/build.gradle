apply plugin: 'com.android.application'
apply plugin: 'testfairy'

buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://www.testfairy.com/maven' }
    }

    dependencies {
        // TestFairy gradle plugin
        classpath 'com.testfairy.plugins.gradle:testfairy:1.+'
    }
}

android {
    /*
    signingConfigs {
        // IMPORTANT: This is just a test config where keystore & password will be readable in GitHub, not to be used with Google Play Store at any circumstances
        signingConfig {
            keyAlias 'key-alias'
            keyPassword 'changeit'
            storeFile file('/Users/antonis/Documents/telestax/code/restcomm-android-sdk/Examples/restcomm-olympus/test-keystore.jks')
            storePassword 'changeit'
        }
    }
    */
    compileSdkVersion 25
    buildToolsVersion '25.0.1'

    dexOptions {
        jumboMode = true
    }

    defaultConfig {
        applicationId "org.restcomm.android.olympus"
        minSdkVersion 16
        targetSdkVersion 25

        if (project.hasProperty('VERSION_CODE')) {
            logger.info("Using versionCode: " + VERSION_CODE)
            versionCode VERSION_CODE.toInteger()
        }
        else {
            logger.info("Doesn't have VERSION_CODE, falling back to: 1")
            versionCode 1
        }

        if (project.hasProperty('VERSION_NAME')) {
            logger.info("Using versionName: " + VERSION_NAME)
            versionName VERSION_NAME
        }
        else {
            versionName "1.0.0-BETA4"
        }

        //buildConfigField "boolean", "DISABLE_SOMETHING", "true"

        // ICE_USERNAME & ICE_PASSWORD should remain secret and hence are defined in global properties (i.e. only available to project members)
        if (project.hasProperty('ICE_USERNAME') && project.hasProperty('ICE_PASSWORD') && project.hasProperty('ICE_DOMAIN')) {
            // Add them as resource values which are available in preferences.xml
            resValue "string", "ICE_USERNAME", ICE_USERNAME
            resValue "string", "ICE_PASSWORD", ICE_PASSWORD
            resValue "string", "ICE_DOMAIN", ICE_DOMAIN
        }
        else {
            // Let's use my personal free xirsys account credentials for community, so that STUN/TURN works out of the box and eases adoption
            resValue "string", "ICE_USERNAME", "atsakiridis"
            resValue "string", "ICE_PASSWORD", "4e89a09e-bf6f-11e5-a15c-69ffdcc2b8a7"
            resValue "string", "ICE_DOMAIN", "cloud.restcomm.com"
        }

        //multiDexEnabled true
        //manifestPlaceholders = [HOCKEYAPP_APP_ID: "9f94a735a6f6431ba368e6e0581067a4"]
    }

    buildTypes {
        release {
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    output.outputFile = new File(output.outputFile.parent, "restcomm-olympus-${variant.versionName}+${variant.versionCode}-RELEASE.apk")
                }
            }

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.signingConfig
            //versionNameSuffix "-BETA4"
        }

        debug {
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    output.outputFile = new File(output.outputFile.parent, "restcomm-olympus-${variant.versionName}+${variant.versionCode}-DEBUG.apk")
                }
            }
            minifyEnabled false
            //versionNameSuffix "-DEBUG"
        }

        /*
        shrunk {
            minifyEnabled true
            shrinkResources true
        }
        */
        // TODO: currently getting some lint error from log4j library we 're using, that we need to fix and then remove this altogether:
        lintOptions {
            abortOnError false
        }
    }

    testfairyConfig {
        if (project.hasProperty('TESTFAIRY_APIKEY')) {
            apiKey TESTFAIRY_APIKEY
        }

        metrics "cpu,memory,network,network-requests,phone-signal,logcat,gps,battery,mic,wifi"
        video "wifi"
        videoRate "0.5"
        videoQuality "low"
        maxDuration "24h"
        recordOnBackground true
        //iconWatermark true
        testersGroups "dev,qa,friends"
        notify true
        //autoUpdate true
        //uploadProguardMapping true
    }

}

repositories {
    jcenter()
    //mavenCentral()
    maven {
        /*
        credentials {
            username ''
            password ''
        }
        */
        //url "https://oss.sonatype.org/content/repositories/staging"
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    maven {
        url 'https://www.testfairy.com/maven'
    }
}

dependencies {
    testCompile 'junit:junit:4.12'
    compile project(':restcomm.android.sdk')
    ////compile 'org.restcomm:restcomm-android-sdk:1.0.0-beta-4.1-SNAPSHOT'
    ////compile('org.restcomm:restcomm-android-sdk:1.0.0-beta-4.1') {
    ////changing = true
    ////}
    compile 'com.android.support:appcompat-v7:25.1.0'
    compile 'com.android.support:design:25.1.0'
    compile 'com.android.support:support-v4:25.1.0'
    compile 'testfairy:testfairy-android-sdk:1.+@aar'
    //compile 'testfairy:testfairy-android-sdk:1.4.5'
    //compile 'net.hockeyapp.android:HockeySDK:4.1.1'
    ////compile files('./libs/testfairy-android-sdk-1.4.6-unlimited-events.jar')
}
